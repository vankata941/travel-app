managed implementation in class zbp_ivi_i_travel unique;
strict;
with draft;

define behavior for zivi_i_travel alias Travel
persistent table zivi_d_travel_a
draft table zivi_d_travel_d
lock master
total etag LocalLastChangedAt
authorization master ( instance )

{
  create;
  update;
  delete;

  side effects {
    entity _Booking affects field TotalPrice;
  }

  // Validations
  validation validateDates on save { field BeginDate, EndDate; create; update; }
  validation validateCustomer on save { field CustomerID; create; update; }

  // Actions
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  // Determinations
  determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
  determination setTravelID on modify { create; }
  determination setInitialStatus on modify { create; }
  determination setInitialCurrency on modify { create; }

  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;

  association _Booking { create; with draft; }


  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelID, OverallStatus, StatusText, TotalPrice,
   CreatedBy, CreatedAt, LastChangedBy, LastChangedAt, LocalLastChangedAt;

  mapping for zivi_d_travel_a
  {
    TravelUUID = travel_uuid;
    TravelID = travel_id;
    AgencyID = agency_id;
    CustomerID = customer_id;
    BeginDate = begin_date;
    EndDate = end_date;
    BookingFee = booking_fee;
    TotalPrice = total_price;
    CurrencyCode = currency_code;
    Description = description;
    OverallStatus = overall_status;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZIVI_I_BOOKING alias Booking
persistent table zivi_d_booking_a
draft table zivi_d_booking_d
lock dependent by _Travel
authorization dependent by _Travel

{
  update;
  delete;

  // Validations
  validation validateFlightDate on save { field FlightDate; create; update; }

  // Determinations
  determination calculateTotalPrice_Book on modify { create; delete; field FlightPrice, CurrencyCode; }
  determination setBookingID on modify { create; }

  association _Travel { with draft; }

  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) BookingID, ParentUUID, LocalLastChangedAt;

  mapping for zivi_d_booking_a
  {
    BookingUUID = booking_uuid;
    ParentUUID = parent_uuid;
    BookingID = booking_id;
    BookingDate = booking_date;
    CarrierID = carrier_id;
    ConnectionID = connection_id;
    FlightDate = flight_date;
    FlightPrice = flight_price;
    CurrencyCode = currency_code;
    LocalLastChangedAt = local_last_changed_at;
  }
}